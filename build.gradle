//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
plugins {
	id "fabric-loom" version "1.5.+"
	id "org.ajoberstar.grgit" version "4.1.1"
	id "maven-publish"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

ext.buildNumber = System.getenv('GITHUB_RUN_NUMBER')
def patch = buildNumber != null ? buildNumber : '99999'

version = version.replace("<patch>", patch)

if (grgit != null) {
	version += "+${grgit.getBranch().current().name}"
} else {
	version += "+custom_unknown"
}

repositories {
	mavenLocal()
	maven { url = "https://maven.quiltmc.org/repository/release" } // Quilt Mappings
	maven { url = "https://maven.parchmentmc.org" } // Parchment
	maven { url = "https://mvn.devos.one/snapshots/" } // Serialization Hooks, ForgeTags
	maven { url = "https://maven.terraformersmc.com/releases/" } // Mod Menu
	maven { url = "https://api.modrinth.com/maven" } // all mods pulled from Modrinth
//	maven { url = "https://hephaestus.dev/release" } // Myron
	maven {
		url = "https://jitpack.io"
		content {
			includeGroup("com.github.Chocohead") // FabricASM
			includeGroup("com.github.LlamaLad7") // old Mixin Extras, for Serialization Hooks
		}
	}
}

configurations.configureEach {
	resolutionStrategy {
		force("net.fabricmc:fabric-loader:$loader_version")
	}
}

dependencies {
    // dev environment
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings(loom.layered {
		// disabled, hashed is somehow leaking through.
//		it.mappings("org.quiltmc:quilt-mappings:$minecraft_version+build.$qm_version:intermediary-v2")
		it.parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
		it.officialMojangMappings { nameSyntheticMembers = false }
	})
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
	modLocalRuntime("maven.modrinth:lazydfu:0.1.2")
	modLocalRuntime("maven.modrinth:suggestionproviderfabric:1.0.1")
//	modLocalRuntime("mcjty.theoneprobe:theoneprobe-fabric:${top_version}")
	modLocalRuntime("maven.modrinth:lithium:${project.lithium_version}")
//	modLocalRuntime("maven.modrinth:vmp-fabric:${project.vmp_version}")
//	modLocalRuntime("maven.modrinth:iris:${project.iris_version}")
//	modLocalRuntime("dev.monarkhes:myron:1.6.3+1.18.1") { transitive = false }
	modLocalRuntime("com.terraformersmc:modmenu:${project.modmenu_version}") { exclude group: "net.fabricmc"; exclude group: "net.fabricmc.fabric-api" }

	// dependencies
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
	modImplementation(include("io.github.tropheusj:serialization-hooks:${project.serialization_hooks_version}"))
	modApi(include("me.alphamode:ForgeTags:${project.forge_tags_version}"))

	modApi(include("com.github.Chocohead:Fabric-ASM:v2.3"))
}

loom {
	accessWidenerPath = file("src/main/resources/porting_lib.accesswidener")

	runs {
		datagen {
			client()

			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
			vmArg "-Dfabric-api.datagen.modid=porting_lib"
			// from ae2

			runDir "build/datagen"
		}

		configureEach {
			vmArg("-Dmixin.debug.export=true")
		}
	}
}

sourceSets {
	main {
		resources {
			srcDir("src/generated/resources")
			exclude("src/generated/resources/.cache")
		}
	}
}

tasks.register("buildOrPublish") {
	group = "build"
	String mavenUser = System.getenv().MAVEN_USER
	if (mavenUser != null && !mavenUser.isEmpty()) {
		dependsOn(tasks.named("publish"))
		println "prepared for publish"
	} else {
		dependsOn(tasks.named("build"))
		println "prepared for build"
	}
}

processResources {
	Map<String, String> properties = new HashMap<>()
	properties.put("version", project.version)
	properties.put("loader_version", project.loader_version)
	properties.put("fabric_version", project.fabric_version)
	properties.put("minecraft_version", project.minecraft_version)
	properties.put("java_version", sourceCompatibility)
	properties.put("class_5124", project.wtf_gradle)
	properties.put("class_56", project.wtf_gradle2)

	properties.forEach((k, v) -> inputs.property(k, v))

	filesMatching("fabric.mod.json") {
		expand properties
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = Integer.parseInt(sourceCompatibility)
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		maven {
			url = "https://mvn.devos.one/${System.getenv().PUBLISH_SUFFIX}/"
			credentials {
				username = System.getenv().MAVEN_USER
				password = System.getenv().MAVEN_PASS
			}
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}
